#lang rhombus/static/and_meta
import:
  lib("esterel/kernel.rkt"):
    expose #{present?}
  lib("esterel/private/kernel.rkt") as pkernel:
    expose #{mk-signal/args}
    expose #{esterel/proc}
    expose #{par/proc}
    expose #{with-trap/proc}
    expose #{run-and-kill-signals!}
    expose #{no-init}
  lib("syntax/location.rkt"):
    expose #{quote-srcloc}

export:
  def_signal
  esterel
  ¿
  |||
  par
  emit
  react
  with_trap
  exit_trap
  with_signal
  pause
  signal_name
  signal_index

meta syntax_class SignalDefinition
| '[$id_expr, ~memoryless, ~init $init_expr, ~combine $combine_expr]':
    field id = id_expr
    field init = init_expr
    field combine = combine_expr
    field memoryless = #true
| '[$id_expr, ~init $init_expr, ~combine $combine_expr]':
    field id = id_expr
    field init = init_expr
    field combine = combine_expr
    field memoryless = #false
| '[$id_expr, ~combine $combine_expr]':
    field id = id_expr
    field init =' #{no-init}'
    field combine = combine_expr
    field memoryless = #false
| '[$id_expr, ~single]':
    field id = id_expr
    field init = '#{no-init}'
    field combine = '#'single'
    field memoryless = #false
| '$id_expr':
    field id = id_expr
    field init = '#{no-init}'
    field combine = #false
    field memoryless = #false

defn.macro 'def_signal {$(signal_definition :: SignalDefinition), ...}':
  def [srclocs, ...] = [expr_meta.pack_s_exp(['#{quote-srcloc}', signal_definition.id]), ...]
  'def $signal_definition.id : #{mk-signal/args}(#'$signal_definition.id, $signal_definition.init, $signal_definition.combine, $srclocs, $signal_definition.memoryless)
   ...'

expr.macro 'esterel:
              $body':
  '#{esterel/proc}(0, fun (): $body)'

operator ¿ x:
  ~stronger_than |||
  #{present?}(x)

expr.macro '$left ||| $right':
   '#{par/proc}(PairList(fun (): $left, fun(): $right))'

fun
| emit(s): kernel.emit(s)
| emit(s,v): kernel.emit(s,v)

fun react(r, ~emit: signals = []):
  // the for loop here converts an equal
  // hash into an equal-always hash in
  // order to cooperate better with Rhombus
  def rkt_signals = PairList(&signals)
  for Map:
    each (k,v): kernel.#{react!}(r, ~emit: rkt_signals) :~ Map
    values(k,v)

expr.macro 'with_trap $t:
              $body':
  '#{with-trap/proc}(#'$t,fun ($t):
                            $body)'
  
expr.macro 'with_signal {$(signal_definition :: SignalDefinition), ...}:
              $body':
  def [srclocs, ...] = [expr_meta.pack_s_exp(['#{quote-srcloc}', signal_definition.id]), ...]
  'block:
     def $signal_definition.id : #{mk-signal/args}(#'$signal_definition.id, $signal_definition.init, $signal_definition.combine, $srclocs, $signal_definition.memoryless)
     ...
     #{run-and-kill-signals!}([$signal_definition.id,...],fun (): $body)'

expr.macro 'par
            | $body
            | ...':
  '#{par/proc}(PairList(&[fun (): $body, ...]))'

expr.macro 'pause':
  'kernel.pause()'

fun exit_trap(t):
  kernel.#{exit-trap}(t)

fun signal_name(s):
  kernel.#{signal-name}(s)

fun signal_index(s):
  kernel.#{signal-index}(s)
