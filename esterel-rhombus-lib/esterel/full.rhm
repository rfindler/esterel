#lang rhombus/static/and_meta
import:
  esterel/kernel open
  lib("esterel/private/full.rkt"):
    expose #{loop-each/proc}
    expose #{loop/proc}
    expose #{await/proc}
    expose #{await-n/proc}
    expose #{await-immediate/proc}
    expose #{every/proc}
    expose #{every-n/proc}
    expose #{every-immediate/proc}

export:
  all_from(esterel/kernel)
  loop
  await
  every
  sustain
  def_strl
  halt

defn.macro 'def_strl $id: $body':
    'def $id: esterel: $body'

fun
| sustain(s): full.sustain(s)
| sustain(s, v): full.sustain(s, v)
  
expr.macro
| 'loop:
     $body
     ...
     ~each $restart':
    '#{loop-each/proc}(fun ():
                         $body
                         ...,
                       fun (): $restart)'
| 'loop:
     $body':
    '#{loop/proc}(fun (): $body)'

expr.macro
| 'await $when ... ~n $n ...':
    ~stronger_than |||
    '#{await-n/proc}(fun (): $when ..., $n ...)'
| 'await ~immediate $when ...':
    '#{await-immediate/proc}(fun (): $when ...)'
| 'await $when ...':
    '#{await/proc}(fun (): $when ...)'

expr.macro
| 'every ~immediate $s ...:
     $body':
    '#{every-immediate/proc}(fun (): $s ..., 
                             fun (): $body)'
| 'every $s ... ~n $n ...:
     $body':
    '#{every-n/proc}(fun (): $s ...,
                     $n ...,
                     fun (): $body)'
| 'every $s ...:
     $body':
    '#{every/proc}(fun (): $s ...,
                   fun (): $body)'

fun halt():
  full.halt()
